Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Товары.Записать();
	Движения.Управленческий.Записать();
	
	Движения.Управленческий.Записывать = Истина;
	Движения.ВзаиморасчетыСКонтрагентами.Записывать = Истина;
	Движения.Товары.Записывать = Истина;
	Движения.Расходы.Записывать = Истина;
	Движения.Доходы.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РеализацияТоваровИУслугТовары.Номенклатура.Ссылка КАК Номенклатура,
	               |	РеализацияТоваровИУслугТовары.Номенклатура.ТипНоменклатуры.Ссылка КАК НоменклатураТипНоменклатуры,
	               |	СУММА(РеализацияТоваровИУслугТовары.Количество) КАК Количество,
	               |	СУММА(РеализацияТоваровИУслугТовары.Сумма) КАК Сумма,
	               |	РеализацияТоваровИУслугТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	               |	МИНИМУМ(РеализацияТоваровИУслугТовары.НомерСтроки) КАК НомерСтроки
	               |ПОМЕСТИТЬ ВТ_ТоварыРеализация
	               |ИЗ
	               |	Документ.РеализацияТоваровИУслуг.Товары КАК РеализацияТоваровИУслугТовары
	               |ГДЕ
	               |	РеализацияТоваровИУслугТовары.Ссылка.Ссылка = &Документ
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РеализацияТоваровИУслугТовары.Номенклатура.Ссылка,
	               |	РеализацияТоваровИУслугТовары.Номенклатура.ТипНоменклатуры.Ссылка,
	               |	РеализацияТоваровИУслугТовары.Номенклатура.Представление
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ТоварыРеализация.НомерСтроки КАК НомерСтроки,
	               |	ВТ_ТоварыРеализация.Номенклатура.Ссылка КАК Номенклатура,
	               |	ВТ_ТоварыРеализация.НоменклатураПредставление КАК НоменклатураПредставление,
	               |	ВТ_ТоварыРеализация.НоменклатураТипНоменклатуры.Ссылка КАК ТипНоменклатуры,
	               |	ВТ_ТоварыРеализация.Количество КАК Количество,
	               |	ВТ_ТоварыРеализация.Сумма КАК Сумма,
	               |	ЕСТЬNULL(УправленческийОстатки.СуммаОстаток, 0) КАК СуммаОстатокБух,
	               |	ЕСТЬNULL(ТоварыОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	               |	ЕСТЬNULL(УправленческийОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокБух,
	               |	ЕСТЬNULL(ТоварыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток
	               |ИЗ
	               |	ВТ_ТоварыРеализация КАК ВТ_ТоварыРеализация
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Управленческий.Остатки(
	               |				&Дата,
	               |				Счет = &Товары,
	               |				,
	               |				Субконто1 В
	               |					(ВЫБРАТЬ
	               |						ВТ_ТоварыРеализация.Номенклатура КАК Номенклатура
	               |					ИЗ
	               |						ВТ_ТоварыРеализация КАК ВТ_ТоварыРеализация)) КАК УправленческийОстатки
	               |		ПО ВТ_ТоварыРеализация.Номенклатура.Ссылка = УправленческийОстатки.Субконто1.Ссылка
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Товары.Остатки(
	               |				&Дата,
	               |				Номенклатура.Ссылка В
	               |					(ВЫБРАТЬ
	               |						ВТ_ТоварыРеализация.Номенклатура.Ссылка КАК НоменклатураСсылка
	               |					ИЗ
	               |						ВТ_ТоварыРеализация КАК ВТ_ТоварыРеализация)) КАК ТоварыОстатки
	               |		ПО ВТ_ТоварыРеализация.Номенклатура.Ссылка = ТоварыОстатки.Номенклатура.Ссылка";
	
	
	Запрос.УстановитьПараметр("Документ", Ссылка);
	Запрос.УстановитьПараметр("Дата", МоментВремени());
	Запрос.УстановитьПараметр("Товары", ПланыСчетов.Управленческий.Товары);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ЗаписьВзаиморасчеты = Движения.ВзаиморасчетыСКонтрагентами.Добавить();
	ЗаписьВзаиморасчеты.Период = Дата;
	ЗаписьВзаиморасчеты.ВидДвижения = ВидДвиженияНакопления.Приход;
	ЗаписьВзаиморасчеты.Контрагент = Покупатель;
	ЗаписьВзаиморасчеты.Сумма = СуммаДокумента;
	
	ЗаписьВыручка = Движения.Управленческий.Добавить();
	ЗаписьВыручка.СчетДт = ПланыСчетов.Управленческий.РасчетыСПокупателями;
	ЗаписьВыручка.СчетКт = ПланыСчетов.Управленческий.Выручка;
	ЗаписьВыручка.Период = Дата;
	ЗаписьВыручка.Сумма = СуммаДокумента;
	ЗаписьВыручка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагенты] = Покупатель;
	
	Пока Выборка.Следующий() Цикл
		
		ЗаписьДоходы = Движения.Доходы.Добавить();
		ЗаписьДоходы.Период = Дата;
		ЗаписьДоходы.Ответственный = Ответственный;
		ЗаписьДоходы.Номенклатура = Выборка.Номенклатура;
		ЗаписьДоходы.Количество = Выборка.Количество;
		ЗаписьДоходы.Сумма = Выборка.Сумма;
		
		Если Выборка.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар Тогда
			Если Выборка.Количество > Выборка.КоличествоОстаток Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Товара %1 недостаточно на складе. Требуется %2. В наличии %3.",
				Выборка.НоменклатураПредставление, Выборка.Количество, Выборка.КоличествоОстаток); 
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Поле = "Товары[" + (Выборка.НомерСтроки - 1) + "].Количество";
				Сообщение.Сообщить();
				Отказ = Истина;
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстаток Тогда
				Себестоимость = Выборка.СуммаОстаток;
			Иначе
				Себестоимость = Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстаток;
			КонецЕсли;
			
			Если Выборка.Количество = Выборка.КоличествоОстатокБух Тогда
				СебестоимостьБух = Выборка.СуммаОстатокБух;
			Иначе
				СебестоимостьБух = Выборка.СуммаОстаток * Выборка.Количество / Выборка.КоличествоОстатокБух;
			КонецЕсли;
			
			
			ЗаписьТовары = Движения.Товары.Добавить();
			ЗаписьТовары.ВидДвижения = ВидДвиженияНакопления.Расход;
			ЗаписьТовары.Период = Дата;
			ЗаписьТовары.Номенклатура = Выборка.Номенклатура;
			ЗаписьТовары.Количество = Выборка.Количество;
			ЗаписьТовары.Сумма = Себестоимость;
			
			ЗаписьРасходы = Движения.Расходы.Добавить();
			ЗаписьРасходы.Период = Дата;
			ЗаписьРасходы.Номенклатура = Выборка.Номенклатура;
			ЗаписьРасходы.Сумма = Себестоимость;
			
			ЗаписьУправленческий = Движения.Управленческий.Добавить();
			ЗаписьУправленческий.СчетДт = ПланыСчетов.Управленческий.Расходы;
			ЗаписьУправленческий.СчетКт = ПланыСчетов.Управленческий.Товары;
			ЗаписьУправленческий.Период = Дата;
			ЗаписьУправленческий.Сумма = СебестоимостьБух;
			ЗаписьУправленческий.Количество = Выборка.Количество;
			ЗаписьУправленческий.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Номенклатура] = Выборка.Номенклатура;
			
		КонецЕсли; 
		
	КонецЦикла;
	
	
КонецПроцедуры
